##################################################################
### Implementación básica de números complejos y sus funciones ###
##################################################################

Complex: record =>
    real => 0
    im => 0

instance:
    add: func(z) =>
        <- !{Complex z'real + real z'im + im}
    endfn

    mul: func(z) =>
        <- !{Complex (z'real * real - z'im * im) (z'real * im + real * z'im)}
    endfn

    abs: func() =>
        <- real * real + im * im
    endfn
endrc

main: func() =>
    y => -1
    while y < 1:
        x => -2.05
        while x < 0.55:
            z => !{Complex}
            i => 0
            while i <= 100:
                z => !(!(z'mul z)'add !{Complex x y})
                i => i + 1
            endwl
            if !z'abs < 2:
                !(print "#")
            else:
                !(print ":")
            endif
            x => x + 0.03
        endwl
        !(printLine "")
        y => y + 0.05
    endwl
endfn

!main
